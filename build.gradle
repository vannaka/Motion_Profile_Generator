plugins {
    id 'application'
    id 'java'
    id 'idea'
    id 'eclipse'
}

mainClassName = 'com.mammen.main.Main'
version = 'v3.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // Used to load native libs
    compile 'org.scijava:native-lib-loader:2.3.0'

    // Add compile time libs
    compile files( 'src/resources/lib/Pathfinder/Pathfinder-Java-1.8.jar' )
}

sourceSets {
    // Set top level dir of the java code
    main.java {
        srcDirs = ['src/java']
        include '**/*.java'
    }

    // Set the top level dir of our resources
    main.resources {
        srcDirs = ['src/java', 'src/resources']
        exclude '**/*.java', 'lib'
    }
}

jar {
    // Set up manifest to point the Main-Class attribute to the right class.
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Version': version
    }

    // Copy contents of jar libs into final jar
    from( configurations.compile.collect( { it.isDirectory() ? it : zipTree(it) } ) ) {
        into( '' )
    }

    // Copy native libs into final jar
    from( 'src/resources/lib/Pathfinder/native_libs' ) {
        exclude( '*.jar' )
        into( 'natives' )
    }
}

wrapper {
    // Set the version of our gradle wrapper
    gradleVersion = '4.9'
}